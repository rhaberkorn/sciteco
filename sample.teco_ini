!* TECO.INI *!

!* Set default terminal color scheme *!
EMQ[$SCITECOPATH]/color.tes
:EMQ[$SCITECOPATH]/colors/terminal.tes

!* Load lexer and buffer session libraries *!
EMQ[$SCITECOPATH]/lexer.tes
EMQ[$SCITECOPATH]/session.tes
EMQ[$SCITECOPATH]/opener.tes

!* Automatic lexing and session management using ED hooks *!
@#ED{
  Oadd,edit,close,quit
  !add!
    !* Add code here to execute when a document is added *!
    :Q*+1Oedit
    32,0ED

    !* non-UTF-8 documents are assumed to be in latin1 (8859-1) *!
    EE"N 1024<:C; -A"T 1EE 1;'> J '

    M[lexer.auto]

    !* Set up margins *!
    [_:M[lexer.test.woman]]_"F
      33ESTEXTWIDTH9U.w
      5*Q.w,0ESSETMARGINWIDTHN
      Q.w,2ESSETMARGINWIDTHN
    '

    0,32ED
    !* fall through *!

  !edit!
    !* Add code here to execute when a document is edited *!
    ESGETCOLUMN,4EJ
    

  !close!
    !* Add code here to execute when a document is closed *!
    

  !quit!
    !* Add code here to execute when SciTECO quits *!
    M[session.save]
}
0,32ED

!* Uncomment to enable automatic case folding *!
!!0,8ED

!*
 * Tweak the default font name and size.
 * The size unit is 1pt/100
 *!
!![lexer.font]Monospace 1300U[lexer.font]

!* Enable default function key macros *!
EMQ[$SCITECOPATH]/fnkeys.tes

!* Uncomment if terminal supports OSC-52 clipboards *!
!!0,256ED

!* Uncomment to enable Unicode icons in the Curses UI *!
!!0,512ED

!* Uncomment to tweak the memory limit *!
!!500*1000*1000,2EJ

!*
 * If files are given on the command-line, open them
 * and disable session saving.
 * Otherwise open a buffer session.
 *!
Z"=
  !*
   * Uncomment to use a separate session per VCS repository or
   * working copy (see session.tes):
   *!
  !!M[session.vcs]
  M[session.load]
|
  [session.path] !* disables session saving *!
  M[opener] -EF
'
