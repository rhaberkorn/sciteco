! TECO.INI !

! Set default terminal color scheme !
EMQ[$SCITECOPATH]/color.tes
:EMQ[$SCITECOPATH]/colors/terminal.tes

! Load lexer and buffer session libraries !
EMQ[$SCITECOPATH]/lexer.tes
EMQ[$SCITECOPATH]/session.tes

! Automatic lexing and session management using ED hooks !
@#ED{
  Oadd,edit,close,quit
  !add!
    ! Add code here to execute when a document is added !
    :Q*+1Oedit
    32,0ED

    M[lexer.auto]

    ! Set up margins !
    [_:M[lexer.test.woman]]_"F
      33ESTEXTWIDTH9U.w
      5*Q.w,0ESSETMARGINWIDTHN
      Q.w,2ESSETMARGINWIDTHN
    '

    0,32ED
    ! fall through !

  !edit!
    ! Add code here to execute when a document is edited !
    ESGETCOLUMN,4EJ
    

  !close!
    ! Add code here to execute when a document is closed !
    

  !quit!
    ! Add code here to execute when SciTECO quits !
    M[session.save]
}
0,32ED

! Uncomment to enable automatic case folding !
! 0,8ED !

! Tweak the default font name and size.
  The size unit is 1pt/100 !
! [lexer.font]Monospace 1300U[lexer.font] !

! Uncomment to enable default keyboard macros and function keys !
! EMQ[$SCITECOPATH]/fnkeys.tes !

! Uncomment if XTerm allows clipboard operations !
! 0,256ED !

! Uncomment to tweak the memory limit !
! 500*1000*1000,2EJ !

! If files are given on the command-line, open them
  and disable session saving.
  Otherwise open a buffer session. !
Z"=
  ! Uncomment to use a separate session per VCS repository or
    working copy (see session.tes): !
  ! M[session.vcs] !
  M[session.load]
|
  [session.path] ! disables session saving !
  [.f
    <:L;R 0X.f [* EBQ.f ]* L>
  ].f
  -EF
'
