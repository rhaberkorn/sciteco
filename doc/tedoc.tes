#!/usr/local/bin/sciteco -m
!* tedoc.tes [-C] [--] <output> <input> <sources> *!

0,2EJ !* FIXME: Memory limiting is too slow *!

:EIQ[$SCITECOPATH]/getopt.tes

@[format_header]{
  FD--S .,(:L"S.|Z')@Xa I^J

  EBQ#tm
  I\# GENERATED FROM Q.#sc (\.#sc):^J
  I.SS  Ga -A-10"N I^J '
  :Q.[topics]">
    I.SCITECO_TOPIC  G.[topics]
  '

  EQ.[header]
  J <FR^J^J.br^J;>
  J <
    SMA; .U.t
    1Ua <."=1;' -A"A|1;' -A"V -1Ua 1;' R>
    Qa"S
      Q.tJ
    |
      .,Q.t@Xa
      I^J.B Qa^J
    '
  >
  J <
    S[[MC]]; 1Xa D
    I^J.RI [ Qa ]^J
  >
  J <FR->\(->;>
  J <
    SMCM[|MC];
    .U.t <-A"C|-A-|"N1;'' R>
    .,Q.t@Xa EQa
    <FR| | ;>
    EQ.[header]
    I^J.IR Qa^J
  >
  J <FRS^J^J;>
  J <FR^JS^J;>
  J <FR^J^J^J;>
  J 0A-10"=D'
  ZJ -A-10"=-D'
  EBQ#tm
  G.[header]
  I^J.^J
}

@[format_body]{
  I.LP^J
  <FR^J^J^J.^J.LP^J;>
  J <FS^J-S^J.IP \(bu 4^J;>
  J <S^JMD.S; 0L I.IP  FSS 4^J>
  !* set margin for list paragraphs *!
  J 0U.n <
    Z-.-3"<1;'
    .,.+3:S.LP"S
      Q.n">
        0L I.RE^J 0U.n
      '
    |
      .,.+3:S.IP"S
        Q.n"=0L I.RS^J %.n '
      '
    '
    :L;
  >
  Q.n"> ZJ I^J.RE '
  J <S<MC>; -D I^J -S< -D I^J.I >
  J <FRS^J^J;>
  J <FR^JS^J;>
  EBN#tm
  G.c I^J.^J.^J
}

!* process command-line options *!
[optstring]C M[getopt]"F (0/0) '
:Q[getopt.C]"<
  [comment.start]!* [comment.end]*!
|
  [comment.start]/* [comment.end]*/
'
LR 0X#ou
2LR 0X#tm EBN#tm EB L
[.f
  <:L;R 0X.f EBQ.f EB L>
].f
-EF

I\#
\# AUTOGENERATED FROM Q#tm
\# DO NOT EDIT MANUALLY!!!
\#^J

!* find insertion point *!
:FS^J.TEDOC^J^J"F(0/0)'

EJ-1<
  <
    2U* [*]#sc

    !* extract comment *!
    SQ[comment.start]$;
    .-:Q[comment.start]-1U.#sc
    SQ[comment.end]
    Q.#sc+:Q[comment.start]+1,.-:Q[comment.end]X.c
    EQ.c
    <FS^J^J  ^J  ;>
    J <FS^J[S,]*[M[ ,	],]^J;>
    J 0A-10"N @X.[topics] '
    <0A-10"N 0A- "N 0A-9"N 1; ' ' ' D>
    ZJ <-A-10"N -A- "N -A-9"N 1; ' ' ' -D>

    !* extract header *!
    H:FD^J^J"F ZJ '
    0,.@X.[header]

    EQ.[header]
    :M[format_header]
    EQ.c
    :M[format_body]
  >
  EF
>

2EL EWQ#ou
EX
